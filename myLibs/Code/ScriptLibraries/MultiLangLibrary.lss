'++LotusScript Development Environment:2:5:(Options):0:74
'********************************************
'Library MultiLangLibrary
'Olimpus: Aug 30, 2019
'********************************************
Option Public
Option Declare

Use "DataBaseTools"
Use "ErrorHandler"

'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Sub Initialize
Declare Sub MLL_buildForm (doc As notesdocument)
Declare Function MLL_getDb As NotesDatabase
Declare Function MLL_getTranslateValues (values As Variant) As Variant
Declare Function MLL_getTranslate (strForTranslate As String) As String
Declare Function MLL_getSearchView As NotesView

'++LotusScript Development Environment:2:5:(Declarations):0:10
Private nLine As String
Private Const designElem = "MultiLangLibrary"

Private Const DEFAULT_LANGUAGE = "RUS"
Private Const LANG_DB_SEARCHKEY = "LANGUAGE_DATABASE"
Private Const LANG_DB_DEFAULT_SERVER = "MSK-LotusTask/OTTO/RU"
Private Const LANG_DB_DEFAULT_FILEPATH = "System\langDB.nsf"
Private Const LANG_DEPENDENCY_TAG = "_ld"
Private Const LANG_SEARCH_VIEW_NAME = "byAllTranslatedValues"

Private lang As String
Private langDb As NotesDatabase
Private searchView As NotesView
Private keyPrefixDb As String
Private keyPrefixForm As String

'++LotusScript Development Environment:2:2:Initialize:2:10

Sub Initialize
	On Error GoTo ErrH
	nLine = Chr(10) & Chr(13)

	Dim s As New NotesSession
	lang = s.Getenvironmentstring("localLang")
	If lang = "" Then lang = DEFAULT_LANGUAGE	
		
	GoTo EndH
ErrH:
	Error Err, DesignElem & " -> " & GetThreadInfo(1) & ", c. " & CStr(ErL) & nLine & Error$
	Resume endH
EndH:
End Sub


'++LotusScript Development Environment:2:2:MLL_buildForm:5:8
'********************************************
'Sub MLL_buildForm
'Olimpus: Aug 30, 2019
'********************************************
Sub MLL_buildForm (doc As notesdocument)
	On Error GoTo ErrH

	Dim values As Variant
	Dim newValues As Variant
	keyPrefixDb = doc.Parentdatabase.Replicaid & "~" & doc.Getitemvalue("form")(0)
	keyPrefixForm = doc.Parentdatabase.Replicaid & "~" & doc.Getitemvalue("form")(0)
	ForAll itm In doc.Items
		If Right(itm.Name, 3) = LANG_DEPENDENCY_TAG Then
			values = itm.Values
			newValues = MLL_getTranslateValues(values)
			itm.values = newValues
		End If
	End ForAll

	GoTo EndH
ErrH:
	Error Err, DesignElem  & " -> " & GetThreadInfo(1) & ", c. " & CStr(Erl) & nLine & Error$
EndH:
End Sub


'++LotusScript Development Environment:2:1:MLL_getDb:6:8
'********************************************
'Function MLL_getDb
'NotesDatabase
'Olimpus: Aug 30, 2019
'********************************************
Function MLL_getDb As NotesDatabase
	On Error GoTo ErrH

	If langDb Is Nothing Then Set langDb = DBT_GetDbBySearchKey(LANG_DB_SEARCHKEY)

	Set MLL_getDb = langDB

	GoTo EndH
ErrH:
	Error Err, DesignElem & " -> " & GetThreadInfo(1) & ", c. " & CStr(Erl) & nLine & Error$
EndH:
End Function

'++LotusScript Development Environment:2:1:MLL_getTranslateValues:6:8
'********************************************
'Function MLL_getTranslateValues
'Variant
'Olimpus: Aug 30, 2019
'********************************************
Function MLL_getTranslateValues (values As Variant) As Variant
	On Error GoTo ErrH

	Dim i As Integer
	Dim newValues() As Variant
	ReDim newValues(UBound(values))
	For i = 0 To UBound(values)
		newValues(i) = MLL_getTranslate(values(i))
	Next

	MLL_getTranslateValues = newValues

	GoTo EndH
ErrH:
	Error Err, DesignElem & " -> " & GetThreadInfo(1) & ", c. " & CStr(Erl) & nLine & Error$
EndH:
End Function


'++LotusScript Development Environment:2:1:MLL_getTranslate:6:8
'********************************************
'Function MLL_getTranslate
'String
'Olimpus: Aug 30, 2019
'********************************************
Function MLL_getTranslate (strForTranslate As String) As String
	On Error GoTo ErrH

	Dim lDoc As NotesDocument
	
	Set lDoc = MLL_getSearchView().Getdocumentbykey(keyPrefixDb & "~" & keyPrefixForm & "~" & strForTranslate, True)
	If lDoc Is Nothing Then Set lDoc = MLL_getSearchView().Getdocumentbykey(keyPrefixDb & "~" & strForTranslate, True)
	If lDoc Is Nothing Then Set lDoc = MLL_getSearchView().Getdocumentbykey(strForTranslate, True)
	If lDoc Is Nothing Then Error 7000, "Не найдена языковая настройка для " & strForTranslate
	
	MLL_getTranslate = lDoc.Getitemvalue("string_" & lang)(0)
	
	GoTo EndH
ErrH:
	Error Err, DesignElem & " -> " & GetThreadInfo(1) & ", c. " & CStr(Erl) & nLine & Error$
EndH:
End Function

'++LotusScript Development Environment:2:1:MLL_getSearchView:6:8
'********************************************
'Function MLL_getSearchView
'NotesView
'Olimpus: Aug 30, 2019
'********************************************
Function MLL_getSearchView As NotesView
	On Error GoTo ErrH

	If searchView Is Nothing Then Set searchView = MLL_getDb.Getview(LANG_SEARCH_VIEW_NAME)
	Set MLL_getSearchView = searchView

	GoTo EndH
ErrH:
	Error Err, DesignElem & " -> " & GetThreadInfo(1) & ", c. " & CStr(Erl) & nLine & Error$
EndH:
End Function